buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.2'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

Properties props = new Properties()
props.load(new FileInputStream("${projectDir}/build.properties"))

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.1'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21

        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner 'com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner'

        buildConfigField "String", "CONTACT_EMAIL", '"' + props['contactEmail'] + '"'

    }
    signingConfigs {
        release {
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
            storeFile file(props['storeFile'])
            storePassword props['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            versionNameSuffix '-release'
        }
        debug {
            versionNameSuffix '-debug'
            ext.enableCrashlytics = false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    releaseCompile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.android.support:support-v4:21.0.0'
    compile 'it.sephiroth.android.library.horizontallistview:library:+'
    compile 'joda-time:joda-time:2.3'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.squareup.dagger:dagger:1.2.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile('com.squareup.retrofit:converter-simplexml:1.6.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

}
